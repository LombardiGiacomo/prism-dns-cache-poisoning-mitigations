ctmc

// --- CONSTANTS ---
// Configuration
const int NUMBER_OF_URL_REQUESTS;
const int MAX_QUEUE = NUMBER_OF_URL_REQUESTS + 1;

// DNS Parameters
const double popularity; // Resource Records maintenance in Resolver's cache: (no ttl = 0.0), (low ttl 1.0) up to (high ttl = 1000.0)
const int query_id_range = 65536;
const int port_id_range;

// Rates
const double guess;
const double requests_rate = 1.0;
const double other_legitimate_requests_rate;

// --- DNS-CPM PARAMETERS ---
const int tau = 5; // Count-Min Sketch Threshold: after 5 packets we activate the mitigation mechanism (UDP to TCP).
const double cms_error_rate = 0.01; // CMS Error/Collision Rate (epsilon = e/w): Possibility of the hashes of DNS URL Responses colliding. 
const double benign_noise_rate; // network traffic arriving to resolver: (low = 1.0) up to (high = 1000.0)

// --- FORMULAS ---
formula tc_flag_active = (detection_signal = true); // Mitigation Defense
formula cache_poisoned = (correct_guess = true); // Attack Status


module intruder_machine
	// -- States
	trials : [0..NUMBER_OF_URL_REQUESTS] init 0;
	resolver_replies : [0..NUMBER_OF_URL_REQUESTS] init 0;

	// -- Transitions
	// 1. Send Request: Initiates resolution.
	[DNS_Request_Client_To_Resolver] trials < NUMBER_OF_URL_REQUESTS -> requests_rate : (trials' = trials + 1);

	// 2. Receive Reply: Marks end of query cycle.
	[DNS_Response_Resolver_To_Client] resolver_replies < trials -> (resolver_replies' = resolver_replies + 1);

	// 3. Termination
	[] resolver_replies = NUMBER_OF_URL_REQUESTS -> true;
endmodule


module victim_resolver
	// -- States
	ttl : [0..2] init 2; // 0=Miss, 1=Hit, 2=Init
	client_queue : [0..MAX_QUEUE] init 0;
	root_queue : [0..MAX_QUEUE] init 0;
	domain_queue : [0..MAX_QUEUE] init 0;
	responses_queue : [0..MAX_QUEUE] init 0;

	// -- Variables
	query_to_root_server: bool init false;
	query_to_domain_server: bool init false;
	answer_from_domain_received: bool init false;
	correct_guess: bool init false;

	// -- Transitions
	// 1. Client Request (Merged Hit/Miss Logic)
	[DNS_Request_Client_To_Resolver] (client_queue < MAX_QUEUE) & (responses_queue < MAX_QUEUE) & (root_queue < MAX_QUEUE) -> 
		// Branch 1: Cache Hit
		(popularity / 10) : (client_queue' = client_queue + 1) & (ttl' = 1) & (responses_queue' = responses_queue + 1)
		+
		// Branch 2: Cache Miss
		(1 - (popularity / 10)) : (client_queue' = client_queue + 1) & (ttl' = 0) & (root_queue' = root_queue + 1);
	
	// 2. Query Root
	[DNS_Request_Resolver_To_Root] (client_queue > 0) & (root_queue > 0) 
		-> (query_to_root_server' = true);

	// 3. Root Responds -> Move to Domain Queue
	[DNS_Response_Root_To_Resolver] (root_queue > 0) & (domain_queue < MAX_QUEUE)
		-> (root_queue' = root_queue - 1) & (domain_queue' = domain_queue + 1) & (query_to_root_server' = false);

	// 4. Query Domain (Opens Vulnerability Window)
	[DNS_Request_Resolver_To_Domain] domain_queue > 0 -> (query_to_domain_server' = true);

	// 5. Domain Responds (Closes Window)
	[DNS_Response_Domain_To_Resolver] (domain_queue > 0) & (responses_queue < MAX_QUEUE) & (correct_guess = false) 
		-> (domain_queue' = domain_queue - 1) & (responses_queue' = responses_queue + 1) & (query_to_domain_server' = false)  & (answer_from_domain_received' = true);
	
	// 6. Reply to Client
	[DNS_Response_Resolver_To_Client] (responses_queue > 0) & (client_queue > 0) -> (client_queue' = client_queue - 1) & (responses_queue' = responses_queue - 1);
	

	// -- Attack Race Condition
    	[Guess] (correct_guess = false) & (query_to_domain_server = true) & (tc_flag_active = false) -> 
		1 / (query_id_range * port_id_range) : (correct_guess'=true)
		+
		((query_id_range * port_id_range) - 1) / (query_id_range * port_id_range): (correct_guess'=false);

    	// -- Sink State
	[] cache_poisoned = true -> true;
endmodule


module root_server
	// -- States
	root_state : [0..1] init 0;
	
	// -- Transitions
	// 1. Receive Request: A query arrives from the Victim Resolver.
	[DNS_Request_Resolver_To_Root] root_state = 0 -> (root_state' = 1);

	// 2. Send Reply: The Root Server immediately responds, moving the resolver to the next step (Domain Server).
	[DNS_Response_Root_To_Resolver] root_state = 1 -> (root_state' = 0);
endmodule


module target_domain_server
	// -- States
	domain_state : [0..1] init 0;

	// -- Transitions
	// 1. Receive Request: Opens Race Window
	[DNS_Request_Resolver_To_Domain] domain_state = 0 -> (domain_state' = 1);

	// 2. Legitimate Response: Closes Race Window
	[DNS_Response_Domain_To_Resolver] domain_state = 1 -> 1 / (other_legitimate_requests_rate) : (domain_state' = 0);

	// 3. Busy Work ("Noise")
	[DNS_Response_Domain_Other_Request] domain_state = 1 -> (other_legitimate_requests_rate - 1) / other_legitimate_requests_rate : (domain_state' = 1);
endmodule


module intruder_server
	// -- States
	is_state : [0..1] init 0;
	
	// -- Transitions
	// 1. Start the attack
	[DNS_Request_Resolver_To_Domain] is_state = 0 -> (is_state' = 1);

	// 2. Firing Loop
	[Guess] is_state = 1 -> guess : (is_state' = 1);

	// 3. Stop the attack
	[DNS_Response_Domain_To_Resolver] is_state = 1 -> (is_state' = 0);
endmodule

module dns_cpm_module
    	// Count-Min Sketch counter
    	suspicious_count : [0..tau] init 0;
    
    	// Signal to the Mitigation Module (True if threshold reached)
    	detection_signal : bool init false;

    	// 1. Detection of a Possible Attack
    	[Guess] suspicious_count < tau -> 
    	    (suspicious_count' = suspicious_count + 1) & 
    	    (detection_signal' = (suspicious_count + 1 >= tau));

    	// 2. Detection of Benign Noise: Normal traffic towards the victim resolver. If a hash collision occurs, increment counter.
    	[Benign_Response] suspicious_count < tau -> 
    		// Case 1: Collision occurs
    		cms_error_rate * ((benign_noise_rate - 1) / benign_noise_rate) : 
			(suspicious_count' = suspicious_count + 1) & (detection_signal' = (suspicious_count + 1 >= tau))
    		+
    		// Case 2: No Collision
    		(1 - cms_error_rate) * (1 / benign_noise_rate): (suspicious_count' = suspicious_count) & (detection_signal' = detection_signal);

   	// 3. Detection of a Legitimate Domain Response: Counts towards the threshold (Same Domain)
	[DNS_Response_Domain_To_Resolver] suspicious_count < tau -> 
    		(suspicious_count' = suspicious_count + 1) & 
    		(detection_signal' = (suspicious_count + 1 >= tau));
endmodule